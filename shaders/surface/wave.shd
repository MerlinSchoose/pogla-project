#version 450

layout (local_size_x = 8, local_size_y = 8) in;

uniform float anim_time;

layout (std430, binding = 1) buffer position_buffer {
    float pos[];
};

layout (std430, binding = 2) buffer normals_buffer {
    float normals[];
};

float wave_height(float x, float z, float t) {
    float h = 0.0f;
    vec2 p = vec2(x, z);
    vec2 dir = vec2(1.0, 0);
    float speed = 0.5;
    float frequency = 0.05;
    float A = 2.8;
    float angle = 0.0;
    for (int i = 0; i < 10; ++i) {
        dir = vec2(cos(angle), sin(angle));
        h += sin(t * speed + dot(dir, p) * frequency) * A;
        //p += dir * cos(t * speed + dot(dir, p) * frequency) * A * 4;
        speed *= 1.0;
        frequency *= 1.2;
        A *= 0.8;
        angle += 12.0;
    }
    return h;
}

void main() {
    uint index = gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x + gl_GlobalInvocationID.x;
    vec3 p = vec3(pos[3 * index], pos[3 * index + 1], pos[3 * index + 2]);

    float diff = 2e-3;

    float h = wave_height(p.x, p.z, anim_time);
    float h1 = wave_height(p.x + diff, p.z, anim_time);
    float h2 = wave_height(p.x, p.z + diff, anim_time);

    vec3 p0 = vec3(p.x, h, p.z);
    vec3 p1 = vec3(p.x + diff, h1, p.z);
    vec3 p2 = vec3(p.x, h2, p.z + diff);
    vec3 n = normalize(cross(p1 - p0, p2 - p0));
    if (n.y < 0)
        n = -n;

    pos[3 * index + 1] = h;
    normals[3 * index] = n.x;
    normals[3 * index + 1] = n.y;
    normals[3 * index + 2] = n.z;
}
